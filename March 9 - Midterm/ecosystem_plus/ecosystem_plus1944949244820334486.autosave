Mover[] movers = new Mover[50]; // fireflies
ArrayList<LightSource> LightSources = new ArrayList<LightSource>(); // light sources

void setup() {
  size(1200, 900);
  String mode = "dark";
  // LightSources.add(new LightSource(width/2, height/2));

  for (int i = 0; i < movers.length; i++) {
    // Each Mover is initialized randomly.
    movers[i] = new Mover(random(width), random(height)); // initial location
  }
}

void draw() {
  background(0,0,0);
  // fill(255, 50);
  // rect(0, 0, width, height);

  if (LightSources.size() == 3) {
    fill(255,0,0);
    text("You reached the maximum number of light sources permitted.", 5,5);
  }

  // For each mover
  for (int i = 0; i < movers.length; i++) {
    // Apply the attraction force from the LightSources on this mover
    for (int j = 0; j < LightSources.size(); j++) {
      LightSources.get(j).display();
      PVector aForce = LightSources.get(j).seek(movers[i]);
      movers[i].applyForce(aForce);
    }

    // Now apply the force from all the other movers on this mover
    for (int j = 0; j < movers.length; j++) {
      // Don’t attract yourself!
      if (i != j) {
        PVector force = movers[j].attract(movers[i]);
        movers[i].applyForce(force);
      }
    }

    movers[i].update();
    movers[i].display();
    // movers[i].checkEdges();
  }
}

void mousePressed() {
  if (LightSources.size() < 3) LightSources.add(new LightSource(mouseX, mouseY));
}

void keyPressed() {
  if (key == 'n' || key == 'N') {

  }
}


class LightSource {
  float mass;
  PVector location;
  float maxspeed;

  LightSource(int _x_, int _y_) {
    location = new PVector(_x_, _y_);
    mass = 30;
    maxspeed = 5;
  }

  // Our seek steering force algorithm
  PVector seek(Mover m) {
    PVector desired = PVector.sub(location, m.location);
    desired.normalize();
    desired.mult(maxspeed);
    PVector steer = PVector.sub(desired, m.velocity);
    steer.limit(0.01); // maxforce
    return (steer);
  }

  void display() {
    fill(255,255,153);
    ellipse(location.x, location.y, mass*2, mass*2);
  }
}



class Mover {
  PVector location;
  PVector velocity;
  PVector acceleration;
  float mass;
  float G = 0.4;
  float myColor;

  Mover(float _x_, float _y_) {
    mass = random(0.01, 2.55);
    location = new PVector(_x_, _y_);
    velocity = new PVector(0, 0);
    acceleration = new PVector(0, 0);
    myColor = mass * 100;
  }

  // Newton’s second law.
  void applyForce(PVector force) {
    // Receive a force, divide by mass, and add to acceleration.
    PVector f = PVector.div(force, mass);
    acceleration.add(f);
  }

  // The Mover now knows how to attract another Mover.
  PVector attract(Mover m) {
    PVector force = PVector.sub(location, m.location);
    float distance = force.mag();
    distance = constrain(distance, 0, 25.0);
    force.normalize();
    float strength = (G * mass * m.mass) / (distance * distance);
    force.mult(strength);
    force.limit(50);
    return force;
  }

  void update() {
    velocity.add(acceleration);
    location.add(velocity);
    acceleration.mult(0);
  }

  void display() {
    fill (myColor);
    // Rotate the mover to point in the direction of travel
    // Translate to the center of the move
    pushMatrix();
    translate(location.x, location.y);
    rotate(velocity.heading());
    triangle(0, 5, 0, -5, 20, 0);
    popMatrix();
  }

  // With this code an object bounces when it hits the edges of a window.
  // Alternatively objects could vanish or reappear on the other side
  // or reappear at a random location or other ideas. Also instead of
  // bouncing at full-speed vehicles might lose speed. So many options!
  void checkEdges() {
    if (location.x > width) {
      location.x = width;
      velocity.x *= -1; // full velocity, opposite direction
      velocity.x *= 0.8; // lose a bit of energy in the bounce
    } else if (location.x < 0) {
      location.x = 0;
      velocity.x *= -1; // full velocity, opposite direction
      velocity.x *= 0.8; // lose a bit of energy in the bounce
    }

    if (location.y > height) {
      location.y = height;
      velocity.y *= -1; // full velocity, opposite direction
      velocity.y *= 0.8; // lose a bit of energy in the bounce
    } else if (location.y < 0) {
      location.y = 0;
      velocity.y *= -1; // full velocity, opposite direction
      velocity.y *= 0.8; // lose a bit of energy in the bounce
    }
  }
}
